apply plugin: 'java'
apply plugin: 'maven-publish'

subprojects {

    apply plugin: 'java'
    apply plugin: 'maven-publish'

    group 'implario'
    version '1.1.2'

    compileJava.options.encoding 'UTF-8'
    tasks.withType(JavaCompile) {
        options.encoding 'UTF-8'
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
        testImplementation 'org.junit.platform:junit-platform-commons:1.7.0'
        testImplementation 'org.junit.platform:junit-platform-engine:1.7.0'

        testImplementation 'com.google.code.gson:gson:2.8.6'

        testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
        testCompileOnly 'org.projectlombok:lombok:1.18.12'

        annotationProcessor 'org.projectlombok:lombok:1.18.12'
        compileOnly 'org.projectlombok:lombok:1.18.12'
    }

    jar {
        from sourceSets.main.resources
        from {
            duplicatesStrategy = DuplicatesStrategy.EXCLUDE
            configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
        }
    }

    def javaComponent = components.getByName('java')

    publishing {
        repositories {
            maven {
                String credentialsStr = System.getenv("MAVEN_CREDENTIALS")
                if (credentialsStr == null) return
                String[] args = credentialsStr.split(" ")
                url = args[0]
                credentials {
                    username = args[1]
                    password = args[2]
                }
            }
        }
        publications {
            register 'maven', MavenPublication, {
                groupId = project.group
                artifactId = project.name
                version = project.version
                from javaComponent
            }
        }
    }
}
